plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

ext {
    PUBLISH_GROUP_ID = 'com.propellerads'
    PUBLISH_VERSION = "${publishVersion}"
    PUBLISH_ARTIFACT_ID = 'sdk'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

task clearJar(type: Delete) {
    delete "${project.buildDir}/libs/" + "sdk-${publishVersion}"
}

task makeJar(type: Copy) {
    from("${project.buildDir}/intermediates/aar_main_jar/release/")
    into("${project.buildDir}/libs/")
    include('classes.jar')
    rename('classes.jar', "sdk-${publishVersion}.jar")
}

makeJar.dependsOn(clearJar, build)

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"

android {
    compileSdk 30
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 30
        versionCode 3
        versionName "0.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    // Android
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.lifecycle:lifecycle-process:2.3.0'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2'

    // Network
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.3"

    // Other
    implementation "androidx.startup:startup-runtime:1.1.0"
    implementation "androidx.browser:browser:1.3.0"

    // Test
    testImplementation 'junit:junit:4.13.2'
    testImplementation "io.mockk:mockk:1.12.2"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
