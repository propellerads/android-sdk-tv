apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'signing'

def jarFile = file("${project.buildDir}/libs/sdk-${publishVersion}.jar")

artifacts {
    archives androidSourcesJar
    archives jarFile
}

artifactory {
    contextUrl = "http://artifactory01.rtty.in:8081/artifactory"
    publish {
        repository {
            repoKey = "ext-snapshot-local"
            username = "${artifactoryLogin}"
            password = "${artifactoryPassword}"
            maven = true
        }  
        authentication {
            basic(BasicAuthentication)
        }   
        defaults {
            publications ('release')
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
                artifact jarFile

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'PropellerAds SDK for Android TV'
                    url = 'https://github.com/propellerads/android-sdk-tv'
                    licenses {
                        license {
                            name = 'Apache License 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'propellerads'
                            name = 'PropellerAds'
                            email = 'noc@propellerads.net'
                        }
                        
                    }

                    scm {
                        connection = 'scm:github.com/propellerads/android-sdk-tv.git'
                        developerConnection = 'scm:git:ssh://github.com/propellerads/android-sdk-tv.git'
                        url = 'https://github.com/propellerads/android-sdk-tv/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
